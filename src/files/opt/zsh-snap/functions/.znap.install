#!/bin/zsh
# install executables & completion functions
# args: <repo> ...
emulate -L zsh; setopt $_znap_opts
zmodload -Fa zsh/files b:zf_ln b:zf_mkdir

if (( $# < 1 )); then
  print -u2 'znap install: not enough arguments'
  .znap.help install
  return $(( sysexits[(i)USAGE] + 63 ))
fi

.znap.clone $@

local REPLY=
private -a files=() executables=() compfuncs=() autofuncs=()
private datadir=${XDG_DATA_HOME:-~/.local/share}/zsh/site-functions
private bindir=~/.local/bin comp= func= link= orig= pat= repo= sub=
mkdir -pm 0700 $bindir $datadir
for repo in $@; do
  repo=~[$repo]

  files=( $repo/{,bin/}*~*.*(-*:A) )
  for orig in $files[@]; do
    link=$bindir/$orig:t
    if [[ $link:A != $orig ]]; then
      hash $orig:t=$link
      if zf_ln -fhs "$orig" "$link"; then
        executables+=( $link )
      else
        return $(( sysexits[(i)CANTCREAT] + 63 ))
      fi
    fi
  done

  comp='_*~*.zwc'
  func='[^[:punct:][:upper:]][^.]##'
  pat="$repo/**/($comp|$func)"
  sub="$( git -C $repo submodule foreach --quiet pwd )"
  [[ -n $sub ]] &&
      pat+="~(${(j:|:)${(f)sub}})/*"
  pat+='(-.^D)'
  files=( $~pat )
  for orig in ${files[@]:|executables}; do
    link=$datadir/$orig:t
    if [[ $link:A != $orig ]]; then
      IFS='' read -r < $orig
      if  [[ $orig == */$~func && $REPLY == (\#\!*zsh|autoload |emulate )* ]] ||
          [[ $orig == */$~comp && $REPLY == \#(autoload|compdef)\ * ]]; then
        if zf_ln -fhs "$orig" "$link"; then
          .znap.compile $orig
          case $orig in
            ( */$~comp ) compfuncs+=( $link ) ;;
            ( */$~func ) autofuncs+=( $link ) ;;
          esac
        else
          return $(( sysexits[(i)CANTCREAT] + 63 ))
        fi
      fi
    fi
  done
done

if (( $#executables )); then
  print -P "\nExecutables installed into %F{12}${(D)bindir}%f:"
  cd -q $bindir
  print -cP -- '%F{10}'${^executables[@]:t}'%f*'
  cd -q -
fi
if (( $#autofuncs )); then
  print -P "\nAutoloadable functions installed into %F{12}${(D)datadir}%f:"
  cd -q $datadir
  print -nP '%F{10}'
  print -c -- ${autofuncs[@]:t}
  print -nP %f
  cd -q -
fi
if (( $#compfuncs )); then
  print -P "\nCompletion functions installed into %F{12}${(D)datadir}%f:"
  cd -q $datadir
  print -c -- ${compfuncs[@]:t}
  cd -q -
fi

hash -f
[[ -f $_comp_dumpfile ]] &&
    zf_rm -f $_comp_dumpfile

true
